if(rootProject.ext.isDebugType){
    apply plugin: 'com.android.application'
}else{
    apply plugin: 'com.android.library'
    apply from: "update-jcenter.gradle"
    apply from: "aar-build.gradle"
}

def getVersionCode(String version){
    return Integer.parseInt(version.replace('.',''))
}

android {
    compileSdkVersion  rootProject.ext.android.compileSdkVersion
    buildToolsVersion  rootProject.ext.android.buildToolsVersion

    defaultConfig {
        if (rootProject.ext.isDebugType){
            applicationId "com.houxj.generaltools"
        }

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode getVersionCode(rootProject.ext.android.versionNumber)
        versionName rootProject.ext.android.versionNumber

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        if (rootProject.ext.isDebugType){
            manifestPlaceholders = [TEST_ACT_CATEGORY: "android.intent.category.LAUNCHER" ]
        }else{
            resourcePrefix 'gentools_'
            manifestPlaceholders = [TEST_ACT_CATEGORY: "android.intent.category.DEFAULT" ]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:26.1.0'
    }
    //为了能打包成aar以后不需要引用包的地址。这里都把外部包的jar包或者aar包下载了放在工程里
    //pinyin4j implementation 'com.belerweb:pinyin4j:2.5.0'
    implementation files('libs/pinyin4j-2.5.0.jar')
    //Gson implementation 'com.google.code.gson:gson:2.8.1'
    implementation files('libs/gson-2.8.1.jar')
    //ImageLoader implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation files('libs/universal-image-loader-1.9.5.jar')
}
